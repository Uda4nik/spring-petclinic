def generatedSources = "generated-sources" // outside buildDir so that it isn't affected by IDEA exclude of buildDir
def generatedResources = "$buildDir/generated-resources"

sourceSets {
    main {
        java.srcDir generatedSources
        output.dir generatedResources
    }
}

task generateSources {
    outputs.dir generatedSources

    doLast {
        def outputFile = file("$generatedSources/org/springframework/samples/petclinic/model/BaseEntity.java")
        mkdir(outputFile.parentFile)
        outputFile.text = file("src/main/generator-input/BaseEntity.java").text
    }
}

compileJava.dependsOn(generateSources)

task generateResources {
    outputs.dir generatedResources

    doLast {
        def outputFile = file("$generatedResources/db/hsqldb/initDB.sql")
        mkdir(outputFile.parentFile)
        outputFile.text = file("src/main/generator-input/initHsqlDB.sql").text
    }
}

processResources.dependsOn(generateResources)

clean {
    delete generatedSources
}





